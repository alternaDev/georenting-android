apply plugin: 'com.android.application'
apply plugin: 'com.squareup.sqldelight'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.jakewharton.hugo'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'testfairy'
apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'com.github.ben-manes.versions'

def vCode = 10000

try {
    vCode = Integer.valueOf(System.getenv("TRAVIS_BUILD_NUMBER"))
} catch (e) {
}

def vName = System.getenv("TRAVIS_TAG")

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()

if (!vName) {
    if (System.getenv("TRAVIS_COMMIT")) {
        vName = vCode + "-" + System.getenv("TRAVIS_COMMIT").substring(0, 7)
    } else {
        vName = gitSha + "-Dev"
    }
} else {
    vName = vName.replace("v", "")
}

android {
    compileSdkVersion 24
    buildToolsVersion '24.0.1'

    adbOptions {
        timeOutInMs 600000 // set timeout to 1 minute
    }

    dexOptions {
        preDexLibraries = false
    }

    dataBinding {
        enabled = true
    }

    signingConfigs {
        debug {
            storeFile file("../keystore.jks")
            storePassword georentingKeystorePassword
            keyAlias "georenting"
            keyPassword georentingKeystorePassword
        }
        release {
            storeFile file("../keystore.jks")
            storePassword georentingKeystorePassword
            keyAlias "georenting"
            keyPassword georentingKeystorePassword
        }
    }

    defaultConfig {
        signingConfig signingConfigs.release
        minSdkVersion 18
        targetSdkVersion 24

        versionCode vCode + 1000
        versionName "${vName}"

        resConfigs "en", "de"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        multiDexEnabled true
    }



    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            zipAlignEnabled true

            signingConfig signingConfigs.debug
            applicationIdSuffix '.debug'
        }
    }

    productFlavors {
        internal {
            minSdkVersion 21 // Use version 21 here so multidex can optimize this.
            applicationId 'de.alternadev.georenting.internal'
        }
        production {
            applicationId 'de.alternadev.georenting'
        }
    }

    lintOptions {
        abortOnError false
        textReport true
        textOutput 'stdout'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testfairyConfig {
        apiKey testfairyKey
        autoUpdate true
        video "wifi"
        videoRate "0.5"
        videoQuality "low"
        maxDuration "15m"
        recordOnBackground true
        uploadProguardMapping true
        iconWatermark true
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')

    compile 'com.android.support:multidex:1.0.1'

    apt "org.parceler:parceler:1.1.5"
    compile 'org.parceler:parceler-api:1.1.5'

    apt 'com.github.hotchemi:permissionsdispatcher-processor:2.1.3'

    compile 'com.android.support:support-v4:24.2.0'
    androidTestCompile 'com.android.support:support-v4:24.2.0'
    compile 'com.android.support:appcompat-v7:24.2.0'
    androidTestCompile 'com.android.support:appcompat-v7:24.2.0'
    compile 'com.android.support:cardview-v7:24.2.0'
    compile 'com.android.support:recyclerview-v7:24.2.0'
    androidTestCompile 'com.android.support:recyclerview-v7:24.2.0'
    compile 'com.android.support:design:24.2.0' //TODO: There seems to be a bug with LayoutParam Casting.
    androidTestCompile 'com.android.support:design:24.2.0'
    compile 'com.android.support:customtabs:24.2.0'
    androidTestCompile 'com.android.support:support-annotations:24.2.0'

    compile 'de.hdodenhof:circleimageview:2.1.0'
    compile 'com.satsuware.lib:usefulviews:2.3.6'

    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'io.reactivex:rxjava:1.1.10'
    compile 'io.reactivex:rxjava-async-util:0.21.0'

    compile 'com.jakewharton.timber:timber:4.3.0'
    compile 'com.jakewharton:process-phoenix:1.0.2'

    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:converter-moshi:2.1.0'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.1.0'
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.4-beta2'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta2'
    compile 'com.squareup.okhttp3:okhttp:3.4.1'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.jakewharton.picasso:picasso2-okhttp3-downloader:1.0.2'
    compile 'com.squareup.sqlbrite:sqlbrite:0.7.0'
    compile 'com.squareup.moshi:moshi:1.2.0'

    compile 'com.facebook.stetho:stetho:1.3.1'
    compile 'com.facebook.stetho:stetho-okhttp3:1.3.1'

    compile 'com.google.dagger:dagger:2.6.1'
    apt 'com.google.dagger:dagger-compiler:2.6.1'
    testCompile 'com.google.dagger:dagger:2.6.1'
    testApt 'com.google.dagger:dagger-compiler:2.6.1'

    compile 'com.jakewharton.byteunits:byteunits:0.9.1'

    compile 'com.google.android.gms:play-services-auth:9.4.0'
    compile 'com.google.android.gms:play-services-gcm:9.4.0' // Still used for GcmNetworkManager
    compile 'com.google.android.gms:play-services-maps:9.4.0'
    compile 'com.google.android.gms:play-services-location:9.4.0'
    compile 'com.google.firebase:firebase-auth:9.4.0'
    compile 'com.google.firebase:firebase-analytics:9.4.0'
    compile "com.google.firebase:firebase-messaging:9.4.0"
    compile 'com.google.firebase:firebase-invites:9.4.0'
    compile 'com.google.firebase:firebase-crash:9.4.0'
    compile 'com.google.firebase:firebase-ads:9.4.0'
    compile 'com.google.firebase:firebase-config:9.4.0'

    compile 'rebus:header-view:1.1.6'

    compile 'com.github.hotchemi:permissionsdispatcher:2.1.3'

    retrolambdaConfig 'net.orfjackal.retrolambda:retrolambda:2.3.0'

    compile 'com.google.auto.value:auto-value:1.3'

    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
    androidTestCompile 'com.android.support.test.espresso:espresso-contrib:2.2.2'
    androidTestCompile 'tools.fastlane:screengrab:0.5.2'

    androidTestCompile 'org.mockito:mockito-core:1.10.19'
    androidTestCompile 'com.google.dexmaker:dexmaker:1.2'
    androidTestCompile 'com.google.dexmaker:dexmaker-mockito:1.2'
    androidTestCompile 'com.android.support.test.uiautomator:uiautomator-v18:2.1.2'
}

def installAll = tasks.create('installAll')
installAll.description = 'Install all applications.'
android.applicationVariants.all { variant ->
    installAll.dependsOn(variant.install)
    // Ensure we end up in the same group as the other install tasks.
    installAll.group = variant.install.group
}

/*task grantPermissions(type: Exec, dependsOn: 'installProductionDebug') {
    try {
        def rootDir = project.rootDir
        def localProperties = new File(rootDir, "local.properties")
        if (localProperties.exists()) {
            Properties properties = new Properties()
            localProperties.withInputStream {
                instr -> properties.load(instr)
            }
            def sdkDir = properties.getProperty('sdk.dir')
            def adb = "$sdkDir/platform-tools/adb"
            def pm = "de.alternadev.georenting.debug"
            if(!(sdkDir == null || sdkDir.equals(""))) {
                commandLine "$adb shell pm grant $pm android.permission.ACCESS_FINE_LOCATION".split(" ")
            }
            //commandLine "$adb shell pm grant $pm android.permission.ACCESS_COARSE_LOCATION".split(" ")
        }
    } catch(Exception e) {
        // Do nothing...
    }
}

afterEvaluate {
    // When launching tests from Android Studio, it seems that only the assemble tasks
    // get called directly, not the install* versions
    tasks.each { task ->
        if (task.name.startsWith('assembleProductionDebugAndroidTest')) {
            task.dependsOn grantPermissions
        }
    }
}*/

apply plugin: 'com.google.gms.google-services'
